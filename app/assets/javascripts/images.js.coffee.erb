$ ->
  insertAtCaret = (target, str) ->
    obj = $(target)
    obj.focus()
    if target.selectionStart?
      text = obj.val()
      position = target.selectionStart
      currentPosition = position + str.length
      obj.val text.substr(0, position) + str + text.substr(position)
      target.setSelectionRange currentPosition, currentPosition
    else
      range = document.selection.createRange()
      range.text = str
      range.select()

  $('.editor-textarea').each ->
    $(@).fileupload
      dropZone: @
      url: '<%= Rails.application.routes.url_helpers.images_path format: :json %>'
      sequentialUploads: true
      dataType: 'json'
      done: (e, data) ->
        $.each data.files, (index, file) ->
          text = '![' + file.name + '](' + data.result.image_url + ')'
          insertAtCaret(e.target, text)
      error: (e) =>
        alert JSON.parse(e.responseText).image
